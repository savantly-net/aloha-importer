plugins {
	id 'org.springframework.boot' version '2.3.3.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
}

group = 'net.savantly.aloha'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

bootJar {
	layered()
	exclude("**/application-local.properties")
}
// run `./gradlew :sprout-modules-forms:dev -Psuspend=y` to suspend for debugger connection
def suspend = project.ext.properties.suspend
if(!suspend) {
	suspend = 'n'
}
task dev(type: JavaExec) {
    group = "Execution"
    description = "Run the app class with JavaExecTask"
    //classpath = sourceSets.test.runtimeClasspath
    classpath += sourceSets.main.runtimeClasspath
    main = 'net.savantly.aloha.importer.ImporterApplication'
    jvmArgs=[
  	  "-agentlib:jdwp=transport=dt_socket,server=y,suspend=$suspend,address=18080",
	  "-Dspring.profiles.active=local"
    ]
}

dependencies {
	implementation platform('software.amazon.awssdk:bom:2.14.3')

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-rest'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.data:spring-data-rest-hal-explorer'
	implementation 'org.springframework.cloud:spring-cloud-aws-messaging:2.2.6.RELEASE'
	
	implementation 'org.hibernate:hibernate-ehcache'
					
	implementation 'software.amazon.awssdk:s3'
	implementation 'software.amazon.awssdk:sns'
	
	implementation 'com.github.albfernandez:javadbf:1.11.2'
	
	compileOnly 'org.projectlombok:lombok'
	
	runtimeOnly 'org.postgresql:postgresql'
	runtimeOnly 'com.h2database:h2'
	//runtimeOnly 'org.hsqldb:hsqldb'
	
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}

bootRun {
  jvmArgs=["-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=18080"]
}

bootJar {
	layered()
	exclude("**/application-local.properties")
}

bootBuildImage {
	imageName = "savantly/${project.name}"
}